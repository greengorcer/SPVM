class ShortList {
  use Fn;
  use Array;
  
  # Enumerations
  enum {
    DEFAULT_CAPACITY = 4,
  }
  
  # Fields
  has capacity : ro int;
  has length : ro int;
  has values : ro short[];
  
  # Class methods
  static method new : ShortList ($array = undef : short[], $capacity = -1 : int) {
    my $length : int;
    if ($array) {
      $length = @$array;
    }
    else {
      $length = 0;
    }
    
    my $self = &new_len($length, $capacity);
    
    if ($array) {
      Array->memcpy_short($self->{values}, 0, $array, 0, $length);
    }
    
    return $self;
  }
  
  static method new_len : ShortList ($length : int, $capacity = -1 : int) {
    my $self = new ShortList;
    
    unless ($length >= 0) {
      die "The \$length must be greater than or equal to 0";
    }
    
    if ($capacity < 0) {
      $capacity = &DEFAULT_CAPACITY;
    }
    
    if ($length > $capacity) {
      $capacity = $length;
    }
    
    $self->{capacity} = $capacity;
    $self->{length} = $length;
    $self->{values} = new short[$capacity];
    
    return $self;
  }
  
  # Instance methods
  method get : int ($index : int) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      die "The \$index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The \$index must be less than the length of the \$list";
    }
    
    my $value = $self->{values}[$index];
    
    return $value;
  }
  
  method insert : void ($index : int, $value : int) {
    my $length = $self->{length};
    
    unless ($index >= 0) {
      die "The \$index must be greater than or equal to 0";
    }
    
    unless ($index <= $length) {
      die "The \$index must be less than or equal to the length of the \$list";
    }
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    my $values = $self->{values};
    if ($index != $length) {
      my $dist_index = $index + 1;
      my $move_length = $length - $index;
      Array->memmove_short($values, $dist_index, $values, $index, $move_length);
    }
    $values->[$index] = (short)$value;
    
    $self->{length} = $new_length;
  }
  
  method pop : int () {
    my $length = $self->length;
    
    unless ($length > 0) {
      die "The length of the \$list must be greater than 0";
    }
    
    my $value = $self->{values}[$length - 1];
    
    $self->{values}[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method push : void ($value : int) {
    my $length = $self->{length};
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    $self->{values}[$length] = (short)$value;
    
    $self->{length} = $new_length;
  }
  
  method remove : int ($index : int) {
    my $length = $self->{length};
    
    unless ($index >= 0) {
      die "The \$index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The \$index must be less than the length of the \$list";
    }
    
    my $values = $self->{values};
    my $remove_value = $values->[$index];
    
    my $dist_index = $index;
    my $src_index = $index + 1;
    my $move_length = $length - $index - 1;
    Array->memmove_short($values, $dist_index, $values, $src_index, $move_length);
    
    $self->{length}--;
    
    return $remove_value;
  }
  
  method replace : void ($offset : int, $remove_length : int, $replace : short[]) {
    unless ($offset >= 0) {
      die("The \$offset must be greater than or equal to 0");
    }
    
    unless ($remove_length >= 0) {
      die("The \$remove_length must be greater than or equal to 0");
    }
    unless ($offset + $remove_length <= $self->{length}) {
      die("The \$offset + the \$removing lenght must be less than or equal to the length of the \$list");
    }
    
    my $replace_length = 0;
    if ($replace) {
      $replace_length = @$replace;
    }
    
    my $new_length = $self->{length} - $remove_length + $replace_length;
    $self->_maybe_extend($new_length);
    
    my $move_length = $self->{length} - $offset - $remove_length;
    Array->memmove_short($self->{values}, $offset + $replace_length, $self->{values}, $offset + $remove_length, $move_length);
    
    if ($replace) {
      Array->memcpy_short($self->{values}, $offset, $replace, 0, $replace_length);
    }
    
    $self->{length} = $new_length;
  }
  
  method reserve : void ($new_capacity : int) {
    unless ($new_capacity >= 0) {
      die "The \$new_capacity must be greater than or equal to 0";
    }
    
    my $capacity = $self->{capacity};
    
    if ($new_capacity > $capacity) {
      my $length = $self->{length};
      my $new_values = new short[$new_capacity];
      Array->memcpy_short($new_values, 0, $self->{values}, 0, $length);
      $self->{values} = $new_values;
      $self->{capacity} = $new_capacity;
    }
  }
  
  method resize : void ($new_length : int) {
    unless ($new_length >= 0) {
      die "The \$new_length must be greater than or equal to 0";
    }
    
    my $length = $self->{length};
    
    if ($new_length > $length) {
      $self->_maybe_extend($new_length);
      Array->memset_short($self->{values}, 0, $length, $new_length - $length);
    }
    elsif ($new_length < $length) {
      Array->memset_short($self->{values}, 0, $new_length, $length - $new_length);
    }
    
    $self->{length} = $new_length;
  }
  
  method set : void ($index : int, $value : int) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      die "The \$index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The \$index must be less than the length of the \$list";
    }
    
    $self->{values}[$index] = (short)$value;
  }
  
  method set_array : void ($array : short[]) {
    unless ($array) {
      die "The \$array must be defined";
    }
    
    my $cur_length = $self->length;
    
    my $set_length = @$array;
    
    unless ($set_length == $cur_length) {
      die "The length of the \$array must be the \$same as the length of the \$list";
    }
    
    Array->memcpy_short($self->{values}, 0, $array, 0, $cur_length);
  }
  
  method shift : short () {
    my $length = $self->{length};
    
    unless ($length > 0) {
      die "The length of the \$list must be greater than 0";
    }
    
    my $values = $self->{values};
    
    my $value = $values->[0];
    
    Array->memmove_short($values, 0, $values, 1, $length - 1);
    
    $values->[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method to_array : short[] () {
    my $length = $self->length;
    
    my $array = new short[$length];
    
    my $values = $self->{values};
    
    Array->memcpy_short($array, 0, $values, 0, $length);
    
    return $array;
  }
  
  method unshift : void ($value : int) {
    my $length = $self->{length};
    
    my $new_length = $length + 1;

    $self->_maybe_extend($new_length);
    
    my $values = $self->{values};
    
    Array->memmove_short($values, 1, $values, 0, $length);
    
    $values->[0] = (short)$value;
    $self->{length} = $new_length;
  }
  
  private method _maybe_extend : void ($min_capacity : int) {
    my $capacity = $self->{capacity};
    
    unless ($min_capacity > $capacity) {
      return;
    }
    
    if ($capacity < $min_capacity) {
      $capacity = $min_capacity;
    }
    
    my $new_capacity = $capacity * 2;
    my $new_values = new short[$new_capacity];
    
    my $length = $self->{length};
    my $values = $self->{values};
    Array->memcpy_short($new_values, 0, $values, 0, $length);
    
    $self->{values} = $new_values;
    $self->{capacity} = $new_capacity;
  }
}
