class FloatList {
  use Fn;
  use Array;
  
  # Fields
  has length   : ro int;
  has values   : ro float[];
  
  # Private fields
  has capacity : int;
  
  # Class methods
  static method new : FloatList ($array : float[]...) {
    my $self = new FloatList;
    my $length : int;
    if ($array) {
      $length = @$array;
    }
    else {
      $length = 0;
    }
    
    my $capacity = $length;
    $self->{capacity} = $capacity;
    my $values = new float[$capacity];
    $self->{length} = $length;
    
    if ($array) {
      Array->memcpy_float($values, 0, $array, 0, $length);
    }
    
    $self->{values} = $values;
    
    return $self;
  }

  static method new_len : FloatList ($length : int) {
    my $self = new FloatList;
    unless ($length >= 0) {
      die "The length must be greater than or equal to 0";
    }
    my $capacity = $length;
    $self->{capacity} = $capacity;
    $self->{length} = $length;
    $self->{values} = new float[$capacity];
    return $self;
  }
  
  # Instance methods
  method get : float ($index : int) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The index must be less than the length";
    }
    
    my $value = $self->{values}[$index];
    
    return $value;
  }
  
  method insert : void ($index : int, $value : float) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index <= $length) {
      die "The index must be less than or equal to the length of the array";
    }
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    my $values = $self->{values};
    if ($index != $length) {
      my $dist_index = $index + 1;
      my $move_length = $length - $index;
      Array->memmove_float($values, $dist_index, $values, $index, $move_length);
    }
    $values->[$index] = $value;
    
    $self->{length}++;
  }

  method pop : float () {
    my $length = $self->length;
    
    unless ($length > 0) {
      die "The length of the array must be greater than 0";
    }
    
    my $value = $self->{values}[$length - 1];
    
    $self->{values}[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method push : void ($value : float) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    $self->{values}[$length] = $value;
    
    $self->{length}++;
  }
  
  method remove : float ($index : int) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The index must be less than the length of the array";
    }
    
    my $values = $self->{values};
    my $remove_value = $values->[$index];
    
    my $dist_index = $index;
    my $src_index = $index + 1;
    my $move_length = $length - $index - 1;
    Array->memmove_float($values, $dist_index, $values, $src_index, $move_length);
    
    $self->{length}--;
    
    return $remove_value;
  }
  
  method resize : void ($new_length : int) {
    unless ($new_length >= 0) {
      die "The new length must be greater than or equal to 0";
    }
    
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($new_length > $length) {
      $self->_maybe_extend($new_length);
      Array->memset_float($self->{values}, $length, 0, $new_length - $length);
    }
    elsif ($new_length < $length) {
      Array->memset_float($self->{values}, $new_length, 0, $length - $new_length);
    }
    
    $self->{length} = $new_length;
  }
  
  method set : void ($index : int, $value : float) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The index must be less than the length";
    }
    
    $self->{values}[$index] = $value;
  }
  
  method set_array : void ($array : float[]) {
    unless ($array) {
      die "The array must be defined";
    }
    
    my $cur_length = $self->length;
    
    my $set_length = @$array;
    
    unless ($set_length == $cur_length) {
      die "The length of the array must be same as the length of the current array";
    }
    
    Array->memcpy_float($self->{values}, 0, $array, 0, $cur_length);
  }
  
  method shift : float () {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($length > 0) {
      die "The length of the array must be greater than 0";
    }
    
    my $values = $self->{values};
    
    my $value = $values->[0];
    
    Array->memmove_float($values, 0, $values, 1, $length - 1);
    
    $values->[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method to_array : float[] () {
    my $length = $self->length;
    
    my $array = new float[$length];
    
    my $values = $self->{values};
    
    Array->memcpy_float($array, 0, $values, 0, $length);
    
    return $array;
  }
  
  method unshift : void ($value : float) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    my $values = $self->{values};
    
    Array->memmove_float($values, 1, $values, 0, $length);
    
    $values->[0] = $value;
    $self->{length}++;
  }
  
  private method _maybe_extend : void ($min_capacity : int) {
    my $capacity = $self->{capacity};
    
    unless ($min_capacity > $capacity) {
      return;
    }
    
    if ($capacity < $min_capacity) {
      $capacity = $min_capacity;
    }
    
    my $new_capacity = $capacity * 2;
    my $new_values = new float[$new_capacity];
    
    my $length = $self->{length};
    my $values = $self->{values};
    Array->memcpy_float($new_values, 0, $values, 0, $length);
    
    $self->{values} = $new_values;
    $self->{capacity} = $new_capacity;
  }
}
