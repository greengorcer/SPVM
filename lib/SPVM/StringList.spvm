class StringList {
  use Fn;
  use Array;
  
  # Fields
  has length : ro int;
  has values : ro string[];
  
  # Private fields
  has capacity : int;
  
  # Class methods
  static method new : StringList ($array : string[]...) {
    my $self = new StringList;
    
    my $length : int;
    if ($array) {
      $length = @$array;
    }
    else {
      $length = 0;
    }
    
    my $capacity = $length;
    my $values = new string[$capacity];
    
    $self->{capacity} = $capacity;
    $self->{length} = $length;
    
    if ($array) {
      Array->memcpy_string_address($values, 0, $array, 0, $length);
    }
    
    $self->{values} = $values;
    
    return $self;
  }
  
  static method new_len : StringList ($length : int) {
    my $self = new StringList;
    unless ($length >= 0) {
      die "The length must be greater than or equal to 0";
    }
    my $capacity = $length;
    $self->{capacity} = $capacity;
    $self->{length} = $length;
    $self->{values} = new string[$capacity];
    return $self;
  }
  
  # Instance methods
  method get : string ($index : int) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The index must be less than the length of the list";
    }
    
    my $value = $self->{values}[$index];
    
    return $value;
  }
  
  method insert : void ($index : int, $value : string) {
    my $length = $self->{length};
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index <= $length) {
      die "The index must be less than or equal to the length of the list";
    }
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    my $values = $self->{values};
    if ($index != $length) {
      my $dist_index = $index + 1;
      my $move_length = $length - $index;
      Array->memmove_string_address($values, $dist_index, $values, $index, $move_length);
    }
    $values->[$index] = $value;
    
    $self->{length} = $new_length;
  }
  
  method pop : string () {
    my $length = $self->length;
    
    unless ($length > 0) {
      die "The length of the list must be greater than 0";
    }
    
    my $value = $self->{values}[$length - 1];
    
    $self->{values}[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method push : void ($value : string) {
    my $length = $self->{length};
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    $self->{values}[$length] = $value;
    
    $self->{length} = $new_length;
  }
  
  method remove : string ($index : int) {
    my $length = $self->{length};
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The index must be less than the length of the list";
    }
    
    my $values = $self->{values};
    my $remove_value = $values->[$index];
    
    my $dist_index = $index;
    my $src_index = $index + 1;
    my $move_length = $length - $index - 1;
    Array->memmove_string_address($values, $dist_index, $values, $src_index, $move_length);
    
    $self->{length}--;
    
    return $remove_value;
  }
  
  method replace : void ($offset : int, $remove_length : int, $replace : string[]) {
    unless ($offset >= 0) {
      die("The offset must be greater than or equal to 0");
    }
    
    unless ($remove_length >= 0) {
      die("The removing length must be greater than or equal to 0");
    }
    unless ($offset + $remove_length <= $self->{length}) {
      die("The offset + the removing lenght must be less than or equal to the length of the list");
    }
    
    my $replace_length = 0;
    if ($replace) {
      $replace_length = @$replace;
    }
    
    my $new_length = $self->{length} - $remove_length + $replace_length;
    $self->_maybe_extend($new_length);
    
    my $move_length = $self->{length} - $offset - $remove_length;
    Array->memmove_string_address($self->{values}, $offset + $replace_length, $self->{values}, $offset + $remove_length, $move_length);
    
    if ($replace) {
      Array->memcpy_string_address($self->{values}, $offset, $replace, 0, $replace_length);
    }
    
    $self->{length} = $new_length;
  }
  
  method resize : void ($new_length : int) {
    unless ($new_length >= 0) {
      die "The new length must be greater than or equal to 0";
    }
    
    my $length = $self->{length};
    
    if ($new_length > $length) {
      $self->_maybe_extend($new_length);
      Array->memset_string($self->{values}, $length, undef, $new_length - $length);
    }
    elsif ($new_length < $length) {
      Array->memset_string($self->{values}, $new_length, undef, $length - $new_length);
    }
    
    $self->{length} = $new_length;
  }
  
  method set : void ($index : int, $value : string) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The index must be less than the length of the list";
    }
    
    $self->{values}[$index] = $value;
  }
  
  method set_array : void ($array : string[]) {
    unless ($array) {
      die "The array must be defined";
    }
    
    my $cur_length = $self->length;
    
    my $set_length = @$array;
    
    unless ($set_length == $cur_length) {
      die "The length of the array must be the same as the length of the list";
    }
    
    Array->memcpy_string_address($self->{values}, 0, $array, 0, $cur_length);
  }
  
  method shift : string () {
    my $length = $self->{length};
    
    unless ($length > 0) {
      die "The length of the list must be greater than 0";
    }
    
    my $values = $self->{values};
    
    my $value = $values->[0];
    
    Array->memmove_string_address($values, 0, $values, 1, $length - 1);
    
    $values->[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method to_array : string[] () {
    my $length = $self->length;
    
    my $array = new string[$length];
    
    my $values = $self->{values};
    
    Array->memcpy_string_address($array, 0, $values, 0, $length);
    
    return $array;
  }
  
  method unshift : void ($value : string) {
    my $length = $self->{length};
    
    my $new_length = $length + 1;

    $self->_maybe_extend($new_length);
    
    my $values = $self->{values};
    
    Array->memmove_string_address($values, 1, $values, 0, $length);
    
    $values->[0] = $value;
    $self->{length} = $new_length;
  }
  
  private method _maybe_extend : void ($min_capacity : int) {
    my $capacity = $self->{capacity};
    
    unless ($min_capacity > $capacity) {
      return;
    }
    
    if ($capacity < $min_capacity) {
      $capacity = $min_capacity;
    }
    
    my $new_capacity = $capacity * 2;
    my $new_values = new string[$new_capacity];
    
    my $length = $self->{length};
    my $values = $self->{values};
    Array->memcpy_string_address($new_values, 0, $values, 0, $length);
    
    $self->{values} = $new_values;
    $self->{capacity} = $new_capacity;
  }
}
