class LongList {
  use Fn;
  use Array;
  
  # Fields
  has length   : ro int;
  has values   : ro long[];
  
  # Private fields
  has capacity : int;
  
  # Class methods
  static method new : LongList ($array : long[]...) {
    my $self = new LongList;
    my $length : int;
    if ($array) {
      $length = @$array;
    }
    else {
      $length = 0;
    }
    
    my $capacity = $length
    $self->{capacity} = $capacity;
    my $values = new long[$capacity];
    
    $self->{length} = $length;
    
    if ($array) {
      Array->memcpy_long($values, 0, $array, 0, $length);
    }
    
    $self->{values} = $values;
    
    return $self;
  }

  static method new_len : LongList ($length : int) {
    my $self = new LongList;
    unless ($length >= 0) {
      die "The length must be greater than or equal to 0";
    }
    my $capacity = $length;
    $self->{capacity} = $capacity;
    $self->{length} = $length;
    $self->{values} = new long[$capacity];
    return $self;
  }
  
  # Instance methods
  method get : long ($index : int) {
    my $length = $self->length;
    
    unless ($index >= 0) {
      die "The index must be greater than or equal to 0";
    }
    
    unless ($index < $length) {
      die "The index must be less than the length";
    }
    
    my $value = $self->{values}[$index];
    
    return $value;
  }
  
  method insert : void ($index : int, $value : long) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($index >= 0 && $index <= $length) {
      die "Out of range";
    }
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    my $values = $self->{values};
    if ($index != $length) {
      my $dist_index = $index + 1;
      my $move_length = $length - $index;
      Array->memmove_long($values, $dist_index, $values, $index, $move_length);
    }
    $values->[$index] = $value;
    
    $self->{length}++;
  }

  method pop : long () {
    my $length = $self->length;
    
    if ($length == 0) {
      die "Can't pop";
    }
    
    my $value = $self->{values}[$length - 1];
    
    $self->{values}[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method push : void ($value : long) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    $self->{values}[$length] = $value;
    
    $self->{length}++;
  }
  
  method remove : long ($index : int) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($index >= 0 && $index < $length) {
      die "Out of range";
    }
    
    my $values = $self->{values};
    my $remove_value = $values->[$index];
    
    my $dist_index = $index;
    my $src_index = $index + 1;
    my $move_length = $length - $index - 1;
    Array->memmove_long($values, $dist_index, $values, $src_index, $move_length);
    
    $self->{length}--;
    
    return $remove_value;
  }
  
  method set : void ($index : int, $value : long) {
    my $length = $self->length;
    
    if ($index < 0 || $index > $length - 1) {
      die "Out of range";
    }
    
    $self->{values}[$index] = $value;
  }
  
  method set_array : void ($array : long[]) {
    unless ($array) {
      die "Array must be defined";
    }
    
    my $cur_length = $self->length;
    
    my $set_length = @$array;
    
    unless ($set_length == $cur_length) {
      die "The length of argument array must be same as the length of current list array";
    }
    
    Array->memcpy_long($self->{values}, 0, $array, 0, $cur_length);
  }
  
  method shift : long () {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($length == 0) {
      die "Can't shift";
    }
    
    my $values = $self->{values};
    
    my $value = $values->[0];
    
    Array->memmove_long($values, 0, $values, 1, $length - 1);
    
    $values->[$length - 1] = 0;
    
    $self->{length}--;
    
    return $value;
  }
  
  method resize : void ($new_length : int) {
    if ($new_length < 0) {
      die "New length must be greater than or equal to 0";
    }
    
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($new_length > $length) {
      $self->_maybe_extend($new_length);
      Array->memset_long($self->{values}, $length, 0, $new_length - $length);
    }
    elsif ($new_length < $length) {
      Array->memset_long($self->{values}, $new_length, 0, $length - $new_length);
    }
    
    $self->{length} = $new_length;
  }
  
  method to_array : long[] () {
    my $length = $self->length;
    
    my $array = new long[$length];
    
    my $values = $self->{values};
    
    Array->memcpy_long($array, 0, $values, 0, $length);
    
    return $array;
  }
  
  method unshift : void ($value : long) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    my $new_length = $length + 1;
    $self->_maybe_extend($new_length);
    
    my $values = $self->{values};
    
    Array->memmove_long($values, 1, $values, 0, $length);
    
    $values->[0] = $value;
    $self->{length}++;
  }
  
  private method _maybe_extend : void ($min_capacity : int) {
    my $capacity = $self->{capacity};
    
    unless ($min_capacity > $capacity) {
      return;
    }
    
    if ($capacity < $min_capacity) {
      $capacity = $min_capacity;
    }
    
    my $new_capacity = $capacity * 2;
    my $new_values = new long[$new_capacity];
    
    my $length = $self->{length};
    my $values = $self->{values};
    Array->memcpy_long($new_values, 0, $values, 0, $length);
    
    $self->{values} = $new_values;
    $self->{capacity} = $new_capacity;
  }
}
